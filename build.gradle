buildscript {
    ext.springBootVersion = '2.0.5.RELEASE'

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
    }
}

allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
}

subprojects {
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'groovy'

    repositories.mavenCentral()

    ext {
        assertjVersion = '3.11.1'
        commonsLang3Version = '3.8'
        commonsTextVersion = '1.3'
        groovyVersion = '2.5.0'
        jacksonVersion = '2.9.6'
        junitVersion = '4.12'
        junit5Version = '5.3.0'
        jsonPathVersion = '2.4.0'
        logbackVersion = '1.2.3'
        mockitoVersion = '2.18.3'
        openpojoVersion = '0.8.10'
        servletVersion = '3.1.0'
        slf4jVersion = '1.7.25'
        spockVersion = '1.1-groovy-2.4'
        springVersion = '5.0.9.RELEASE'
        vavrVersion = '0.9.2'
    }

}


class BuildNumberGeneratorTask extends DefaultTask {
    @TaskAction
    def increaseBuildVersion() {
        File file = new File("build.number")
        Integer number = file.text.toInteger() + 1
        //println "new build version: $number"
        file.write(number.toString())
    }
}

tasks.withType(JavaCompile) {
    options.compilerArgs = ["-proc:none"]  
}
