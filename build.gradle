buildscript {
    ext.springBootVersion = '2.1.1.RELEASE'

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
    }
}

allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
}

subprojects {
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'groovy'

    repositories.mavenCentral()

    ext {
        assertjVersion = '3.11.1'
        commonsLang3Version = '3.8.1'
        commonsTextVersion = '1.3'
        groovyVersion = '2.5.3'
        jacksonVersion = '2.9.7'
        junitVersion = '4.12'
        junit5Version = '5.3.2'
        jsonPathVersion = '2.4.0'
        logbackVersion = '1.2.3'
        mockitoVersion = '2.23.0'
        openpojoVersion = '0.8.10'
        servletVersion = '4.0.1'
        slf4jVersion = '1.7.25'
        spockVersion = '1.2-groovy-2.5'
        springVersion = '5.1.3.RELEASE'
        vavrVersion = '0.9.2'
    }

}


class BuildNumberGeneratorTask extends DefaultTask {
    @TaskAction
    def increaseBuildVersion() {
        File file = new File("build.number")
        Integer number = file.text.toInteger() + 1
        //println "new build version: $number"
        file.write(number.toString())
    }
}

tasks.withType(JavaCompile) {
    options.compilerArgs = ["-proc:none"]  
}
